buildscript {
	repositories {
		mavenCentral();
		jcenter();
		maven {
			url "https://plugins.gradle.org/m2/";
		}
	}
	
	dependencies {
		classpath "com.github.jengelman.gradle.plugins:shadow:2.0.4";
		classpath "gradle.plugin.net.minecrell:licenser:0.3";
	}
}

apply plugin: "java";
apply plugin: "com.github.johnrengelman.shadow";
apply plugin: "net.minecrell.licenser";

group = "io.github.ionch";
archivesBaseName = "IonChannel";
version = "0.0.1";

sourceCompatibility = 1.8;
targetCompatibility = 1.8;

defaultTasks 'clean', 'shadowJar', 'apiJar';

repositories {
	mavenCentral();
	jcenter();
	
	ivy {
		name "endless.blue dependency mirror";
		artifactPattern "https://endless.blue/files/ivy/[module]-[revision].[ext]";
	}
}

configurations {
	api.extendsFrom shadow
	compile.extendsFrom shadow
}

dependencies {
	testCompile "junit:junit:4.12";
	
	shadow  group: "com.google.code.gson",     name: "gson",                version: "2.8.5";
	shadow  group: "com.google.code.findbugs", name: "jsr305",              version: "3.0.2";
	shadow  group: "org.glassfish.grizzly",    name: "grizzly-framework",   version: "2.4.0";
	shadow  group: "org.glassfish.grizzly",    name: "grizzly-http",        version: "2.4.0";
	shadow  group: "org.glassfish.grizzly",    name: "grizzly-http-server", version: "2.4.0";
	shadow  group: "com.google.guava",         name: "guava",               version: "23.5-jre";
	shadow  group: "org.slf4j",                name: "slf4j-api",           version: "1.7.25";
	
	shadow  "blue.endless:Jankson:0.0.4";
}

artifacts {
    archives shadowJar;
}

task apiJar(type: com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar) {
	classifier = "api";
	from(sourceSets.main.allJava) {
        include "social/ionch/api/**";
    }
    
    configurations = [ project.configurations.shadow ];
}

license {
	sourceSets = [project.sourceSets.main]
	
    include '**/*.java';
    
    exclude "/doc/**";
    exclude "**/*.json";
    exclude "**/*.hjson";
    exclude "**/*.png";
}
